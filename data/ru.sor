^0 ноль
1 один
2$ два
2 две
3 три
4 четыре
5 пять
6 шесть
7 семь
8 восемь
9 девять
10 десять
11 одиннадцать
12 двенадцать
13 тринадцать
14 четырнадцать
15 пятнадцать
16 шестнадцать
17 семнадцать
18 восемнадцать
19 девятнадцать
([23])(\d) $1|дцать[ $2]
4(\d) сорок[ $1]
9(\d) девяносто[ $1]
(\d)(\d) $1десят[ $2]
1(\d\d) сто[ $1]
2(\d\d) двести[ $1]
([34])(\d\d) $1ста[ $2]
(\d)(\d\d) $1сот[ $2]
1(\d{3}) одна тысяча[ $1]
([234]|\d?[02-9][234])(\d{3}) $1 тысячи[ $2]
(\d{1,3})(\d{3}) $1 тысяч[ $2]
1(\d{6}) один миллион[ $1]
(\d{1,3})(\d{6}) $1 миллионов[ $2]
1(\d{9}) один миллиард[ $1]
(\d{1,3})(\d{9}) $1 миллиардов[ $2]
1(\d{12}) один триллион[ $1]
(\d{1,3})(\d{12}) $1 триллионов[ $2]
1(\d{15}) один квадриллион[ $1]
(\d{1,3})(\d{15}) $1 квадриллионов[ $2]
1(\d{18}) один квинтиллион[ $1]
(\d{1,3})(\d{18}) $1 квинтиллионов[ $2]
1(\d{21}) один секстилион[ $1]
(\d{1,3})(\d{21}) $1 секстилионов[ $2]
1(\d{24}) один септиллион[ $1]
(\d{1,3})(\d{24}) $1 септиллионов[ $2]

# negative numbers

[-−]1 минус единица
[-−](\d+) минус |$1

# decimals

"1[.,]" одна целая
"([-−]?1)[.,]" $1 целая
"([-−]?\d+)[.,]" $1 целых
"([-−]?\d+[.,])(1)" $1| и одна десятая
"([-−]?0[.,])(\d)" $1| |$2 десятых
"([-−]?\d+[.,])(\d)" $1| и |$2 десятый
"([-−]?\d+[.,])([02-9]1)" $1| и $(f:|$2) сотая
"([-−]?\d+[.,])(\d\d)" $1| и |$2 сотых
"([-−]?\d+[.,])(\d[02-9]1)" $1| и $(f:|$2) тысячная
"([-−]?\d+[.,])(\d11)" $1| и $(f:|$2) тысячных
"([-−]?\d+[.,])(\d{3})" $1| и |$2 тысячный
"([-−]?\d+[.,])(\d)(\d)(\d)(\d)" $1| |$2 |$3 |$4 |$5
"([-−]?\d+[.,]\d*)(\d)" $1| |$2


# female conversion
f:(.*)один \1одна
f:(.*) \1

# currency

# unit/subunit

us:([^,]*),([^,]*),([^,]*),([^,]*),([^,]*),([^,]*) \1
up:([^,]*),([^,]*),([^,]*),([^,]*),([^,]*),([^,]*) \2
ug:([^,]*),([^,]*),([^,]*),([^,]*),([^,]*),([^,]*) \3
ss:([^,]*),([^,]*),([^,]*),([^,]*),([^,]*),([^,]*) \4
sp:([^,]*),([^,]*),([^,]*),([^,]*),([^,]*),([^,]*) \5
sg:([^,]*),([^,]*),([^,]*),([^,]*),([^,]*),([^,]*) \6

EUR:(\D+) $(\1: евро, евро, евро, цент, цента, центов)
GBP:(\D+) $(\1: фунт стерлингов, фунт стерлингов, фунт стерлингов, пенни, пенса, пенсов)
RUB:(\D+) $(\1: рубль, рубля, рублей, копейка, копейки, копеек)
UAH:(\D+) $(\1: гривна, гривны, гривен, копейка, копейки, копеек)
USD:(\D+) $(\1: доллар США, доллара США, долларов США, цент, цента, центов)


"([A-Z]{3}) ([-−]?1)([.,]00?)?" $2|$(\1:us)
"([A-Z]{3}) ([-−]?\d*[02-9]1)([.,]00?)?" $2|$(\1:us)
"([A-Z]{3}) ([-−]?[234])([.,]00?)?" $2|$(\1:up)
"([A-Z]{3}) ([-−]?\d*[02-9][234])([.,]00?)?" $2|$(\1:up)
"([A-Z]{3}) ([-−]?\d+)([.,]00?)?" $2|$(\1:ug)

"((RUB) [-−]?\d+)[.,]([02-9])1" $1 $(\30) одна$(\2:ss)
"(([A-Z]{3}) [-−]?\d+)[.,](01)" $1 |$(1)$(\2:ss)
"((EUR|GBP|USD) [-−]?\d+)[.,]([02-9]2)" $1 $3|$(\2:sp)
"(([A-Z]{3}) [-−]?\d+)[.,]([02-9][234])" $1 |$3$(\2:sp)
"(([A-Z]{3}) [-−]?\d+)[.,](\d)" $1 |$(\30)$(\2:sg)
"(([A-Z]{3}) [-−]?\d+)[.,](\d\d)" $1 |$3$(\2:sg)

== ordinal-feminine ==

0 нулевая
([-−]?\d+) $(ordinal-feminine |$1)

(.*)один	\1первая
(.*)два	\1вторая
(.*)три	\1третья
(.*)ыре	\1вёртая
(.*)осемь	\1осьмая
(.*)мь	\1дьмая
(.*)ь	\1ая
(.*)к	\1ковая
(.*)осемьдесят	\1осьмидесятая
(.*)ьдесят	\1идесятая
(.*)осто	\1остая
(.*)сто	\1сотая
(.*)вести	\1вухсотая
(.*)[еи]ста	\1ёхсотая
(.*)осемьсот	\1осьмисотая
(.*)ьсот	\1исотая
одна\sтысяча	тысячная
(.*)а\sтысяча	\1отысячная
(.*)ве\sтысячи	\1вухтысячная
(.*)[еи]\sтысячи	\1ёхтысячная
(.*)осемь\sтысяч	\1осьмитысячная
(.*)ь\sтысяч	\1итысячная
(.*)к\sтысяч	\1катысячная
(.*)осемьдесят\sтысяч	\1осьмидесятитысячная
(.*)ьдесят\sтысяч	\1идесятитысячная
(.*)о\sтысяч	\1отысячная
(.*)ести\sтысяч	\1ухсоттысячная
(.*)[еи]ста\sтысяч	\1ёхсоттысячная
(.*)осемьсот\sтысяч	\1осьмисоттысячная
(.*)ьсот\sтысяч	\1исоттысячная
один\s(\w+илли\w+)	\1ная
(.*)ин\s(\w+илли\w+)	\1но\2ная
(.*)а\s(\w+илли\w+)а	\1ух\2ная
(.*)[еи]\s(\w+илли\w+)а	\1ёх\2ная
(.*)осемь\s(\w+илли\w+)ов	\1осьми\2ная
(.*)ь\s(\w+илли\w+)ов	\1и\2ная
(.*)к\s(\w+илли\w+)ов	\1ка\2ная
(.*)осемьдесят\s(\w+илли\w+)ов	\1осьмидесяти\2ная
(.*)ьдесят\s(\w+илли\w+)ов	\1идесяти\2ная
(.*)о\s(\w+илли\w+)ов	\1о\2ная
(.*)ести\s(\w+илли\w+)ов	\1ухсот\2ная
(.*)[еи]ста\s(\w+илли\w+)ов	\1ёхсот\2ная
(.*)осемьсот\s(\w+илли\w+)ов	\1осьмисот\2ная
(.*)ьсот\s(\w+илли\w+)ов	\1исот\2ная
(.*)	\1ая

== ordinal-neuter ==

0 нулевой
([-−]?\d+) $(ordinal |$1)

(.*)один	\1первое
(.*)два	\1второе
(.*)три	\1третье
(.*)ыре	\1вёртое
(.*)осемь	\1осьмое
(.*)мь	\1дьмое
(.*)ь	\1ое
(.*)к	\1ковое
(.*)осемьдесят	\1осьмидесятое
(.*)ьдесят	\1идесятое
(.*)осто	\1остое
(.*)сто	\1сотое
(.*)вести	\1вухсотое
(.*)[еи]ста	\1ёхсотое
(.*)осемьсот	\1осьмисотое
(.*)ьсот	\1исотое
одна\sтысяча	тысячное
(.*)а\sтысяча	\1отысячное
(.*)ве\sтысячи	\1вухтысячное
(.*)[еи]\sтысячи	\1ёхтысячное
(.*)осемь\sтысяч	\1осьмитысячное
(.*)ь\sтысяч	\1итысячное
(.*)к\sтысяч	\1катысячное
(.*)осемьдесят\sтысяч	\1осьмидесятитысячное
(.*)ьдесят\sтысяч	\1идесятитысячное
(.*)о\sтысяч	\1отысячное
(.*)ести\sтысяч	\1ухсоттысячное
(.*)[еи]ста\sтысяч	\1ёхсоттысячное
(.*)осемьсот\sтысяч	\1осьмисоттысячное
(.*)ьсот\sтысяч	\1исоттысячное
один\s(\w+илли\w+)	\1ное
(.*)ин\s(\w+илли\w+)	\1но\2ное
(.*)а\s(\w+илли\w+)а	\1ух\2ное
(.*)[еи]\s(\w+илли\w+)а	\1ёх\2ное
(.*)осемь\s(\w+илли\w+)ов	\1осьми\2ное
(.*)ь\s(\w+илли\w+)ов	\1и\2ное
(.*)к\s(\w+илли\w+)ов	\1ка\2ное
(.*)осемьдесят\s(\w+илли\w+)ов	\1осьмидесяти\2ное
(.*)ьдесят\s(\w+илли\w+)ов	\1идесяти\2ное
(.*)о\s(\w+илли\w+)ов	\1о\2ное
(.*)ести\s(\w+илли\w+)ов	\1ухсот\2ное
(.*)[еи]ста\s(\w+илли\w+)ов	\1ёхсот\2ное
(.*)осемьсот\s(\w+илли\w+)ов	\1осьмисот\2ное
(.*)ьсот\s(\w+илли\w+)ов	\1исот\2ное
(.*)	\1ое

== ordinal(-masculine)? ==

0 нулевой
([-−]?\d+) $(ordinal |$1)

(.*)один	\1первый
(.*)два	\1второй
(.*)три	\1третий
(.*)ыре	\1вёртый
(.*)сть	\1стой
(.*)осемь	\1осьмой
(.*)мь	\1дьмой
(.*)ь	\1ый
(.*)к	\1ковой
(.*)осемьдесят	\1осьмидесятый
(.*)ьдесят	\1идесятый
(.*)осто	\1остый
(.*)сто	\1сотый
(.*)вести	\1вухсотый
(.*)[еи]ста	\1ёхсотый
(.*)осемьсот	\1осьмисотый
(.*)ьсот	\1исотый
одна\sтысяча	тысячный
(.*)а\sтысяча	\1отысячный
(.*)ве\sтысячи	\1вухтысячный
(.*)[еи]\sтысячи	\1ёхтысячный
(.*)осемь\sтысяч	\1осьмитысячный
(.*)ь\sтысяч	\1итысячный
(.*)к\sтысяч	\1катысячный
(.*)осемьдесят\sтысяч	\1осьмидесятитысячный
(.*)ьдесят\sтысяч	\1идесятитысячный
(.*)о\sтысяч	\1отысячный
(.*)ести\sтысяч	\1ухсоттысячный
(.*)[еи]ста\sтысяч	\1ёхсоттысячный
(.*)осемьсот\sтысяч	\1осьмисоттысячный
(.*)ьсот\sтысяч	\1исоттысячный
один\s(\w+илли\w+)	\1ный
(.*)ин\s(\w+илли\w+)	\1но\2ный
(.*)а\s(\w+илли\w+)а	\1ух\2ный
(.*)[еи]\s(\w+илли\w+)а	\1ёх\2ный
(.*)осемь\s(\w+илли\w+)ов	\1осьми\2ный
(.*)ь\s(\w+илли\w+)ов	\1и\2ный
(.*)к\s(\w+илли\w+)ов	\1ка\2ный
(.*)осемьдесят\s(\w+илли\w+)ов	\1осьмидесяти\2ный
(.*)ьдесят\s(\w+илли\w+)ов	\1идесяти\2ный
(.*)о\s(\w+илли\w+)ов	\1о\2ный
(.*)ести\s(\w+илли\w+)ов	\1ухсот\2ный
(.*)[еи]ста\s(\w+илли\w+)ов	\1ёхсот\2ный
(.*)осемьсот\s(\w+илли\w+)ов	\1осьмисот\2ный
(.*)ьсот\s(\w+илли\w+)ов	\1исот\2ный
(.*)	\1ый

== ordinal-number-feminine ==

(\d+) \1-я

== ordinal-number-neuter ==

(\d+) \1-е

== ordinal-number(-masculine)? ==

(\d+) \1-й

== help ==

"" |$(1)|, |$(2)|, |$(3)|\n$(help ordinal-feminine)$(help ordinal-masculine)$(help ordinal-neuter)$(help ordinal-number-feminine)$(help ordinal-number-masculine)$(help ordinal-number-neuter)
(.*) \1: |$(\1 1)|, |$(\1 2)|, |$(\1 3)|\n
